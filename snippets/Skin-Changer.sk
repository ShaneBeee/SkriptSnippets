# Simple Skin Changer
# By: ShaneBee

# This little snippet allows you to change a player's skin and their profile name

# Requires: Skript-Reflect and Paper (I should hope you already have Paper)


#! SET SKIN EFFECT !#
# Player = the player who's skin is going to change
# String = the name of the player to grab a skin from
effect set skin of %player% to %string%:
	trigger:
		set {_p} to expr-1
		set {_n} to expr-2

		create new section with {_name}, {_player} stored in {_sec}:
			set {_pro} to server.getServer().createProfile({_name})
			{_pro}.complete(true)
			set {_text} to {_pro}.getTextures()
			set {_pro} to {_player}.getPlayerProfile()
			{_pro}.setTextures({_text})
			return {_pro}
		run section {_sec} async with arguments {_n} and {_p} and store result in {_pro} and wait
		{_p}.setPlayerProfile({_pro})

#! PROFILE NAME EXPRESSION !#
# this allows you to change the player's profile name (name in tablist and above head)
expression [player] profile name of %player%:
	get:
		return (expr-1).getPlayerProfile().getName()
	set:
		set {_player} to expr-1
		set {_name} to change value

		set {_pro} to {_player}.getPlayerProfile()
		{_pro}.setName({_name})
		{_player}.setPlayerProfile({_pro})

#! EXAMPLE USAGE !#
command /setskin <name:string>:
	trigger:
		set profile name of player to {_name}
		set skin of player to {_name}
