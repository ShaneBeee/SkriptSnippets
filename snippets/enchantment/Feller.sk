# Feller
# By: ShaneBee

# This snippet shows how to use a custom enchantment to create a tree feller axe.

# Requirements: Skript 2.9+, SkBee, Beer (Datapack - See ReadME), Minecraft 1.21+

#! FUNCTIONS !#
# This function breaks connected logs/wood
function fell(b: block, p: player, lvl: number, t: timespan):
	set {_l1} to location of {_b} ~ vector(1,1,1)
	set {_l2} to location of {_b} ~ vector(-1,-1,-1)
	loop blocks within {_l1} and {_l2}:
		if all:
			loop-block != {_b}
			type of loop-block is tagged as minecraft block tag "minecraft:logs"
			durability of tool of {_p} > 0
			enchantment level of beer:feller of tool of {_p} = {_lvl}
		then:
			break loop-block with effects and with xp using tool of {_p}

			if gamemode of {_p} = survival or adventure:
				damage tool of {_p} by 1
			if {_t} > 0 ticks:
				wait {_t}
			fell(loop-block, {_p}, {_lvl}, {_t})

#! EVENTS !#
# Handle breaking a log/wood with our enchanted item
on break:
	if type of event-block is tagged as minecraft block tag "minecraft:logs":
		set {_lvl} to enchantment level of beer:feller of player's tool
		if {_lvl} > 0:
			# The time between block breaks is determinded by the level of enchantment
			# The higher the level the faster the blocks break
			# After level 5, it will break instantly
			set {_t} to (6 - {_lvl})
			set {_t} to "%{_t}% ticks" parsed as timespan
			fell(event-block, player, {_lvl}, {_t})
