# Give your dropped items some lovely names
# By: ShaneBee

function setName(e: entity):
	set {_amount} to item amount of item of {_e}
	set {_m::1} to text component from "&b%{_amount}%&7x "
	if name of item of {_e} is set:
		set {_m::2} to text component of name of item of {_e}
	else:
		set {_m::2} to translate component of item of {_e}
		set color format of {_m::2} to light blue

	set {_m} to merge components {_m::*}
	set component entity display name of {_e} to {_m}

on item spawn:
	setName(event-entity)

on item merge:
	set {_f} to future event-entity
	wait 1 tick
	setName({_f})
