# ATTACK PIG
# By: ShaneBee

# This fun little script is used as an example for making a mob into a hostile mob

# REQUIRES: SkBee

#! FUNCTION !#
# Spawn a new attack pig
function spawnAttackPig(l: location):
	spawn a pig at {_l}:
		set boolean tag "custom;attackPig" of nbt of entity to true
		set name of entity to "&cAttack Pig"

#! LISTENER !#
# Checks when entity is loaded and enables attack status
on entity added to world:
	wait 1 tick
	set {_e} to event-entity
	if type of {_e} = pig:
		if nbt of {_e} has tag "custom;attackPig":
			while {_e} is alive:
				set {_p} to nearest player in radius 15 around {_e}
				if gamemode of {_p} = survival or adventure:
					set path target of {_e} to {_p}
					if distance between {_e} and {_p} < 1:
						make {_e} damage {_p} by 1
				wait 5 ticks

#! COMMAND !#
# Spawn an attack pig
command /attackpig:
	trigger:
		set {_l} to location 0.5 above target block
		spawnAttackPig({_l})
