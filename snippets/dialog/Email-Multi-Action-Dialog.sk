# Email System
# By: ShaneBee

# This is a pretty complex email system.
# This allows players to send emails to each other.
# They have the option to read, delete, mark unread, reply.

# Requirements: SkBee 3.16.0+, Paper 1.21.7+

# Do note, this is not intended for production, only for educational purposes

function openEmailMenu(p: player):
	open multi action dialog to {_p}:
		title: "<bold>Email"
		columns: 1
		body:
			if size of {emails::%uuid of {_p}%::*} = 0:
				add plain message body:
					contents: "&cYou have no emails!"
			else:
				add plain message body:
					contents: "Your emails:"
		actions:
			loop reversed indexes of {emails::%uuid of {_p}%::*}:
				set {_n} to {emails::%uuid of {_p}%::%loop-value%}
				set {_from} to offlineplayer(uuid tag "from" of {_n})
				set {_id} to loop-value
				set {_date} to unix date of ({_id} parsed as number)
				set {_subject} to string tag "email_subject" of {_n}

				set {_u} to "  "
				if {_n} has tag "read":
					set {_c} to raw "<gray>"
				else:
					set {_c} to raw "<#68FAA6>"
					if boolean tag "email_urgent" of {_n} = true:
						set {_u} to raw "<#F99803>â„¹"
						set {_c} to raw "<#4EF903>"

				add callback action button:
					label: mini message from "%{_u}% %{_c}%From: %{_from}% Date: %{_date}%"
					tooltip: "Subject: " + {_subject}
					width: 275
					trigger:
						openEmail(player, {_id})
			add callback action button:
				label: "&cDelete All"
				trigger:
					delete {emails::%uuid of player%::*}
					openEmailMenu(player)
			add callback action button:
				label: "&bCreate Email"
				trigger:
					createEmail(player)
			add static action button:
				label: "&eExit"

function openEmail(player: player, id: string):
	set {_n} to {emails::%uuid of {_player}%::%{_id}%}
	set boolean tag "read" of {emails::%uuid of {_player}%::%{_id}%} to true
	set {_fromUUID} to uuid tag "from" of {_n}
	set {_from} to offlineplayer({_fromUUID})
	set {_subject} to string tag "email_subject" of {_n}
	set {_body} to string tag "email_body" of {_n}
	open multi action dialog to {_player}:
		title: "<bold>Email"
		columns: 1
		body:
			add plain message body:
				contents: mini message from "<bold>You got an email from <aqua>%{_from}%<white>:"
			add plain message body:
				contents: mini message from "<bold>Subject:<reset> ""%{_subject}%"""
			add plain message body:
				contents: """%{_body}%"""
		actions:
			# REPLY
			set {_reply} to empty nbt compound
			set uuid tag "from" of {_reply} to (uuid of {_player})
			set uuid tag "to" of {_reply} to {_fromUUID}
			add callback action button:
				label: "Reply"
				trigger:
					set {_to} to offlineplayer({_fromUUID})
					writeEmail(player, {_to})

			# MARK UNREAD
			add callback action button:
				label: "&bMark Unread"
				trigger:
					delete tag "read" of {emails::%uuid of player%::%{_id}%}
					openEmailMenu(player)

			# DELETE
			add callback action button:
				label: "&cDelete"
				trigger:
					delete {emails::%uuid of player%::%{_id}%}
					openEmailMenu(player)

			# EMAILS
			add callback action button:
				label: "Emails"
				trigger:
					openEmailMenu(player)

			# EXIT
			add static action button:
				label: "&eExit"

function createEmail(from: player):
	open multi action dialog to {_from}:
		title: "<bold>Email"
		body:
			add plain message body:
				contents: "Enter a player's name:"
		inputs:
			add text input:
				label: "Player Name:"
				key: "to"
		actions:
			add callback action button:
				label: "Create"
				trigger:
					set {_n} to event-nbt
					set {_name} to string tag "to" of event-nbt
					set {_to} to offlineplayer({_name}, false)
					if all:
						{_to} is set
						player != {_to}
					then:
						writeEmail(player, {_to})
					else:
						noPlayer(player, {_name})

function noPlayer(player: player, to: string):
	open multi action dialog to {_player}:
		title: "<bold>Email"
		columns: 1
		body:
			if name of {_player} = {_to}:
				add plain message body:
					contents: "&cYou cannot email yourself"
			else:
				add plain message body:
					contents: "&cNo player by the name &e%{_to}%"
		actions:
			add callback action button:
				label: "Emails"
				trigger:
					openEmailMenu(player)
			add static action button:
				label: "Exit"

function writeEmail(from: player, to: offlineplayer):
	open multi action dialog to {_from}:
		title: "<bold>Email"
		body:
			add plain message body:
				contents: "Send an email to %{_to}%"
		inputs:
			add text input:
				label: "Subject"
				key: "email_subject"
			add text input:
				label: "Body:"
				key: "email_body"
				max_length: 256
				multiline_max_lines: 20
				multiline_height: 100
			add boolean input:
				label: "Urgent?"
				key: "email_urgent"
		actions:
			add callback action button:
				label: "Send"
				trigger:
					set {_n} to event-nbt
					set uuid tag "to" of {_n} to uuid of {_to}
					set uuid tag "from" of {_n} to uuid of {_from}
					sendEmail(player, {_n})

function sendEmail(from: player, email: nbt compound):
	set {_fromuuid} to uuid of {_from}

	set {_id} to unix timestamp of now
	set {_to} to uuid tag "to" of {_email}
	set {emails::%{_to}%::%{_id}%} to {_email}
	set {_p} to offlineplayer({_to})
	if {_p} is online:
		send "&7[&bEmail&7] &rYou received a new email from %{_from}%... <cmd:/email open>&bOPEN EMAILS!" to {_p}

command /email [<string>] [<string>]:
	trigger:
		if arg-1 = "send":
			set {_to} to offlineplayer(arg-2, false)
			if any:
				player = {_to}
				{_to} is not set
			then:
				noPlayer(player, arg-2)
			else:
				writeEmail(player, {_to})
		else:
			openEmailMenu(player)

on tab complete of "email":
	set tab completions for position 1 to "open", "send"
	if tab arg-1 = "send":
		set tab completions for position 2 to all players
