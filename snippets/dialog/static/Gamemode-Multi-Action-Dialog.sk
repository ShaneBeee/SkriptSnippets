# Gamemode Switcher
# By: ShaneBee

# This is a simple gamemode switcher dialog.

# Do note, this is not intended for production, only for educational purposes

# Requirements: SkBee, SkNMS 1.3.0+, Paper 1.21.6+

registry registration:
	register multi action dialog with id "custom:gamemodes":
		title: "Gamemode"
		can_close_with_escape: true
		columns: 1
		body:
			add plain message body:
				contents: "Choose a gamemode"
		actions:
			loop all gamemodes:
				add static action button:
					label: translate component from "gameMode.%loop-value%"
					action: click event to run command "/gm %loop-value%"

command /gm [<gamemode>]:
	trigger:
		if arg-1 is not set:
			open dialog with id "custom:gamemodes" to player
		else:
			set gamemode of player to arg-1
