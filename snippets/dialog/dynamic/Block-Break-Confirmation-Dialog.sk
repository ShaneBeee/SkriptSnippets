# Block Break Confirmation
# By: ShaneBee

# This script opens a dialog box when a player breaks a block
# asking them to confirm whether to break the block.

# Unlike the registration version of this same script,
# this version opens a dynamic dialog along with the location of the block
# which is passed thru to the event, no variables needed.

# Do note, this is not intended for production, only for educational purposes

# Requirements: SkBee, SkNMS 1.3.0+, Paper 1.21.6+

on mine:
	if gamemode of player is survival:
		set {_n} to nbt from "{}"
		set float tag "x" of {_n} to x coord of event-block
		set float tag "y" of {_n} to y coord of event-block
		set float tag "z" of {_n} to z coord of event-block
		open multi action dialog to player:
			title: "Block Break Confirmation"
			can_close_with_escape: false
			columns: 1
			body:
				add plain message body:
					contents: mini message from "Do you want to break this block?"
			actions:
				add dynamic action button:
					label: mini message from "Confirm Break"
					tooltip: mini message from "I wish for the block to break and drop stuff"
					id: "custom:confirm_break"
					additions: {_n}
				add static action button:
					label: mini message from "Cancel Break"
					tooltip: mini message from "I do not wish for the block to break"
					# No action as we want nothing to happen

on dynamic button click:
	if "%event-namespacedkey%" contains "custom:confirm_break":
		set {_n} to event-nbtcompound
		if {_n} is set:
			set {_x} to float tag "x" of {_n}
			set {_y} to float tag "y" of {_n}
			set {_z} to float tag "z" of {_n}
			set {_loc} to location({_x},{_y},{_z},world of player)
			break block at {_loc} with effects using player's tool
