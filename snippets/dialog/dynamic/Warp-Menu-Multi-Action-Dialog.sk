# Warp Menu
# By: ShaneBee

# This is a simple warp menu with action buttons for each warp.

# Do note, this is not intended for production, only for educational purposes

# Requirements: SkBee, SkNMS 1.3.0+, Paper 1.21.6+

command /warp [<string>]:
	trigger:
		if arg-1 is set:
			teleport player to {warps::%arg-1%}
		else:
			open multi action dialog to player:
				title: mini message from "<gradient:#0ABAF7:#0AF718>Warp Menu"
				body:
					add plain message body:
						contents: "&b&l&nSelect a warp to teleport to:"
				actions:
					loop {warps::*}:
						add static action button:
							label: "%loop-index%"
							action: click event to run command "/warp %loop-index%"

command /setwarp <string>:
	trigger:
		set {warps::%arg-1%} to location of player
