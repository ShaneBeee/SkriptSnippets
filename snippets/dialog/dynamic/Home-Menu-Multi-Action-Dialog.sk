# Home Menu
# By: ShaneBee

# This is a simple home menu with action buttons for each home.

# Do note, this is not intended for production, only for educational purposes

# Requirements: SkBee, SkNMS 1.3.0+, Paper 1.21.6+

command /home [<string>]:
	trigger:
		if arg-1 is set:
			teleport player to {homes::%uuid of player%::%arg-1%}
		else:
			if {homes::%uuid of player%::*} is not set:
				send "&cYou have no homes!"
				stop
			set {_p} to player
			open multi action dialog to player:
				title: mini message from "<gradient:#0ABAF7:#0AF718>Home Menu"
				body:
					add plain message body:
						contents: "&b&l&nSelect a home to teleport to:"
				actions:
					loop {homes::%uuid of {_p}%::*}:
						add static action button:
							label: "%loop-index%"
							action: click event to run command "/home %loop-index%"

command /sethome <string>:
	trigger:
		set {homes::%uuid of player%::%arg-1%} to location of player
