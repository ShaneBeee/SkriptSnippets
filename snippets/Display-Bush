# Display Bush
# By: ShaneBee

# This script is just a fun way of using Display Entities.

# Create custom item
on load:
	set {-bush::item::1} to player head with nbt from "{CustomModelData:12255,SkullOwner:{Id:[I;-1291184209,-1164161754,-1260656384,-1770925678],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzA5ZWMwZDY0NWQwZDVhYTI3MTAwYTg4NmFiNzY0ZjI1ZjdmOWQwYTYwMzQ5ODEzYzQ2N2NmZTBkZGZiZDhlYyJ9fX0=""}]}}}"

#! COMMAND !#
# Give yourself the bush item
command /bush <b:number=1>:
	trigger:
		give player 1 of {-bush::item::%{_b}%}

#! FUNCTIONS !#
# Place a bush
function placeBush(b: block):
	le spawn item display at location 0.5 above {_b}
	set {_e} to last spawned entity
	set display item of {_e} to {-bush::item::1}
	set display scale of {_e} to vector(0,0,0)
	set display translation of {_e} to vector(0,-1,0)
	wait 1 tick
	set interpolation duration of {_e} to 15
	set interpolation start of {_e} to -1
	set display scale of {_e} to vector(2.01,2,2.01)
	set display translation of {_e} to vector(0,0,0)
	set block above {_b} to light[level=0]
	loop 15 times:
		set block state tag "level" of block above {_b} to loop-number
		wait 1 tick
	set block at {_b} to green wool
	set int array tag "custom;bushEntity" of nbt of {_b} to uuid int array of {_e}

# Used when a bush is broken
function breakBush(b: block):
	set {_n} to nbt of {_b}
	set {_e} to uuid tag "custom;bushEntity" of {_n}
	set {_e} to {_e} parsed as entity

	set interpolation start of {_e} to -1
	set interpolation duration of {_e} to 10
	set display scale of {_e} to vector(0,0,0)
	set display translation of {_e} to vector(0,-1,0)
	loop 10 times:
		set block data tag "level" of block above {_b} to (16 - loop-number)
		wait 1 tick
	set block above {_b} to air
	drop 1 of {-bush::item::1} at location 0.45 below {_b} without velocity
	set velocity of last dropped item to vector(0,0.2,0)
	kill {_e}

#! EVENT HANDLERS !#
on place of player wall head:
	if custom model data of player's tool = 12255:
		cancel event

on place of player head:
	if custom model data of player's tool = 12255:
		if past event-block is not air:
			cancel event
			stop
		if block above event-block is not air:
			cancel event
			stop
		loop 15 times:
			set block data tag "rotation" of event-block to loop-number
			wait 1 tick
		set block data tag "rotation" of event-block to 0
		placeBush(block at event-block)

on break of green wool:
	set {_n} to nbt of event-block
	if {_n} has tag "custom;bushEntity":
		clear drops
		breakBush(event-block)
