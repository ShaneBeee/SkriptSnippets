# Sorted Tablist
# By: ShaneBee

# This snippet allows your players to be ordered in the tablist based on their rank
# This snipped may not work with plugins like TAB or any other plugin that messes with vanilla teams

# Requirements = SkBee

#! Setup teams and prefixes for teams !#
on load:
	set team prefix of team named "a-team" to "&7[&bOWNER&7] "
	set team prefix of team named "b-team" to "&7[&aADMIN&7] "
	set team prefix of team named "c-team" to "&7[&6MOD&7] "
	set team prefix of team named "d-team" to "&7[&dVIP&7] "
	# more if you'd like
	set team prefix of team named "z-team" to "&7[&cPLAYER&7] "

#! Simple Function To Rank Player!#
function rankPlayer(p: player):
	if {_p} has permission "rank.owner":
		set {_t} to team named "a-team"
	else if {_p} has permission "rank.admin":
		set {_t} to team named "b-team"
	else if {_p} has permission "rank.mod":
		set {_t} to team named "c-team"
	else:
		set {_t} to team named "z-team"

	# SkBee 2.11.0+
	add {_p} to {_t}

	# SkBee 2.10.0 or below
	add {_p} to team entries of {_t}

on connect:
	rankPlayer(player)
