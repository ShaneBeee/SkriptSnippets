# Simple Item Cooldown
# By: ShaneBee

# Obviously adjust the event as need be
on right click holding any hoe:
	# This means the cooldown is in the future, and the player has to wait
	# Obviously adjust the variable as need be
	if {cooldowns::%uuid of player%::hoe} > now:
		# Stop the player from using the tool
		cancel event
		# We get a simple difference between now and our cooldown, and let the player know how long they need to wait
		set {_diff} to difference between {cooldowns::%uuid of player%::hoe} and now
		send "You have to wait %{_diff}% to use this tool"
	else:
		# If the cooldown isn't set or enough time has passed we allow the player to use the tool then set the cooldown
		set {cooldowns::%uuid of player%::hoe} to 10 seconds from now

on quit:
	# Delete this to prevent clogging up the variable file
	# If your cooldown is for a long time, you may want to keep it
	delete {cooldowns::%uuid of player%::*}
